/* AoE2 Underground - Winter Crown Slapdown */
/* MadCADer Chasm */

<PLAYER_SETUP>

/* Sling nerfs */
#const MOD_RESOURCE 1
#const ATTR_SET_TIME -1
#const TRIBUTE_INEFFICIENCY 46
#const COINAGE 23
effect_percent MOD_RESOURCE TRIBUTE_INEFFICIENCY ATTR_SET 200
effect_amount MODIFY_TECH COINAGE ATTR_SET_TIME 180

/* Boar lame nerf, boar does a little over 1/3 scout hp on hit now */
/* Scouts expected to have <1/3 hp left after lame (hit twice) */
#const BOAR 48
#const ATTR_ATTACK 9
effect_amount SET_ATTRIBUTE BOAR ATTR_ATTACK 7434 /* 29 - eagles */
effect_amount SET_ATTRIBUTE BOAR ATTR_ATTACK 2058 /* 8 - cavalry */
effect_amount SET_ATTRIBUTE BOAR ATTR_ATTACK 7698 /* 30 - camels */

/* res spawns + distances adjusted, copy paste whole map */

/* Chasm (10-16-21) by TheMadCADer */
/* Changes from 1.0: added jungle variation */
/* Changes from 1.1: higher elevations, players closer to middle */
	
start_random
	percent_chance 50
		#define HOT
	percent_chance 50
		#define COLD
end_random
	
if HOT
	#const GROUND 12
	#const WOODS 17
	#const PATCH1 6
elseif COLD
	#const GROUND 32
	#const WOODS 21
	#const PATCH1 3
endif
#const GRAVEL 70
	
<PLAYER_SETUP>
grouped_by_team
	
<LAND_GENERATION>
base_terrain DLC_ROCK
create_player_lands
{
	terrain_type DLC_ROCK
	base_size 18
	land_percent 100
	set_zone_by_team
	other_zone_avoidance_distance 17
	if TINY_MAP
		base_elevation 7
	elseif SMALL_MAP
		base_elevation 8
	elseif MEDIUM_MAP
		base_elevation 9
	elseif LARGE_MAP
		base_elevation 10
	elseif HUGE_MAP
		base_elevation 11
	elseif GIGANTIC_MAP
		base_elevation 12
	endif
	if 2_PLAYER_GAME
	else
		circle_radius 30 10
	endif
}
	
<ELEVATION_GENERATION>
	
<TERRAIN_GENERATION>
create_terrain GROUND
{
	base_terrain DLC_ROCK
	land_percent 100
	number_of_clumps 10000
	set_scale_by_groups
	set_flat_terrain_only
	spacing_to_other_terrain_types 1
	height_limits 1 12
}
create_terrain GROUND
{
	base_terrain DLC_ROCK
	land_percent 100
	number_of_clumps 10000
	set_scale_by_groups
	terrain_mask 1
	if TINY_MAP
		height_limits 5 7
	elseif SMALL_MAP
		height_limits 6 8
	elseif MEDIUM_MAP
		height_limits 7 9
	elseif LARGE_MAP
		height_limits 8 10
	elseif HUGE_MAP
		height_limits 9 11
	elseif GIGANTIC_MAP
		height_limits 10 12
	endif
}
create_terrain WOODS
{
	base_terrain GROUND
	land_percent 15
	number_of_clumps 32
	set_scale_by_groups
	set_avoid_player_start_areas
}
create_terrain GROUND
{
	base_terrain DLC_ROCK
	land_percent 2
	number_of_clumps 32
	set_scale_by_groups
	terrain_mask 1
}
create_terrain PATCH1
{
	base_terrain GROUND
	land_percent 10
	number_of_clumps 24
	set_scale_by_groups
	terrain_mask 1
}
create_terrain GRAVEL
{
	base_terrain GROUND
	land_percent 10
	number_of_clumps 24
	set_scale_by_groups
	terrain_mask 1
}
	
<OBJECTS_GENERATION>
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}
create_object VILLAGER
{
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 6
}
create_object SCOUT
{
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 9
}
if REGICIDE /* regicide objects */
	create_object VILLAGER
	{
		number_of_objects 7
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 6
	}
	create_object KING
	{
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 6
	}
	create_object CASTLE
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 10
	}
endif
create_object GOLD /* primary */
{
	number_of_objects 7
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 12
	min_distance_group_placement 7
	max_distance_to_other_zones 7
	avoid_forest_zone 1
}
create_object STONE /* primary */
{
	number_of_objects 5
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 12
	max_distance_to_players 15
	min_distance_group_placement 7
	max_distance_to_other_zones 7
	avoid_forest_zone 1
}
create_object GOLD /* secondary */
{
	number_of_objects 5
	set_tight_grouping
	group_placement_radius 3
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 16
	max_distance_to_players 18
	min_distance_group_placement 7
	max_distance_to_other_zones 7
	avoid_forest_zone 1
}
create_object STONE /* secondary */
{
	number_of_objects 4
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 20
	max_distance_to_players 26
	min_distance_group_placement 7
	max_distance_to_other_zones 7
	avoid_forest_zone 1
}
create_object GOLD /* tertiary */
{
	number_of_objects 4
	set_tight_grouping
	group_placement_radius 3
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 20
	max_distance_to_players 26
	min_distance_group_placement 7
	max_distance_to_other_zones 7
	avoid_forest_zone 1
}
create_object GOLD /* extra */
{
	number_of_objects 3
	number_of_groups 2
	set_tight_grouping
	group_placement_radius 3
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 20
	min_distance_to_players 30
	max_distance_to_other_zones 7
	avoid_forest_zone 1
}
create_object STONE /* extra */
{
	number_of_objects 3
	number_of_groups 2
	set_tight_grouping
	group_placement_radius 3
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 20
	min_distance_to_players 30
	max_distance_to_other_zones 7
	avoid_forest_zone 1
}
if DEATH_MATCH
else
if HOT
	create_object DLC_LLAMA
else
	create_object SHEEP
endif
{
	number_of_objects 4
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 6
    max_distance_to_players 6
	find_closest
}
if HOT
	create_object DLC_LLAMA
else
	create_object SHEEP
endif
{
	number_of_objects 2
	number_of_groups 2
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 16
	max_distance_to_players 16
}
if HOT
	create_object JAGUAR
else
	create_object WOLF
endif
{
	number_of_objects 3
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 30
	max_distance_to_players 40
}
create_object FORAGE
{
	number_of_objects 6
	set_tight_grouping
	group_placement_radius 4
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 12
	min_distance_group_placement 6
	max_distance_to_other_zones 7
	avoid_forest_zone 1
}
create_object BOAR
{
	number_of_objects 2
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 12
	max_distance_to_players 14
	set_circular_placement
	avoid_forest_zone 1
}
create_object DEER
{
	number_of_objects 4
	set_place_for_every_player
	set_gaia_object_only
	set_loose_grouping
	min_distance_to_players 16
	max_distance_to_players 18
	avoid_forest_zone 1
}
if HOT
	create_object JUNGLETREE
else
	create_object SNOW_PINE_TREE
endif
{
	number_of_objects 2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 4
	max_distance_to_players 5
	min_distance_group_placement 2
}
if HOT
	create_object JUNGLETREE
else
	create_object SNOW_PINE_TREE
endif
{
	number_of_objects 3
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 8
	min_distance_group_placement 2
}
endif
create_object ROCK
{
	number_of_objects 25
	set_scaling_to_map_size
	terrain_to_place_on DLC_ROCK
}
if COLD
	create_object DLC_MOUNTAIN_9
else
	create_object MOUNTAIN_1
endif
{
	number_of_objects 2
	min_distance_group_placement 25
	min_distance_to_players 25
	terrain_to_place_on GROUND
	avoid_forest_zone 3
}
if COLD
	create_object DLC_MOUNTAIN_10
else
	create_object MOUNTAIN_2
endif
{
	number_of_objects 2
	min_distance_group_placement 25
	min_distance_to_players 25
	terrain_to_place_on GROUND
	avoid_forest_zone 3
}
if COLD
	create_object DLC_MOUNTAIN_11
else
	create_object DLC_MOUNTAIN_5
endif
{
	number_of_objects 2
	min_distance_group_placement 25
	min_distance_to_players 25
	terrain_to_place_on GROUND
	avoid_forest_zone 3
}
create_object RELIC
{
	number_of_objects 1
	min_distance_to_players 30
	min_distance_to_map_edge 3
	set_place_for_every_player
	find_closest
	set_gaia_object_only
	if TINY_MAP
		temp_min_distance_group_placement 25
	elseif SMALL_MAP
		temp_min_distance_group_placement 30
	elseif MEDIUM_MAP
		temp_min_distance_group_placement 35
	elseif LARGE_MAP
		temp_min_distance_group_placement 40
	elseif HUGE_MAP
		temp_min_distance_group_placement 45
	elseif GIGANTIC_MAP
		temp_min_distance_group_placement 50
	endif
}
