/* AoE2 Underground - Winter Crown Slapdown */
/* MadCADer Frozen Cove */

<PLAYER_SETUP>

/* Sling nerfs */
#const MOD_RESOURCE 1
#const ATTR_SET_TIME -1
#const TRIBUTE_INEFFICIENCY 46
#const COINAGE 23
effect_percent MOD_RESOURCE TRIBUTE_INEFFICIENCY ATTR_SET 200
effect_amount MODIFY_TECH COINAGE ATTR_SET_TIME 180

/* Boar lame nerf, boar does a little over 1/3 scout hp on hit now */
/* Scouts expected to have <1/3 hp left after lame (hit twice) */
#const BOAR 48
#const ATTR_ATTACK 9
effect_amount SET_ATTRIBUTE BOAR ATTR_ATTACK 7434 /* 29 - eagles */
effect_amount SET_ATTRIBUTE BOAR ATTR_ATTACK 2058 /* 8 - cavalry */
effect_amount SET_ATTRIBUTE BOAR ATTR_ATTACK 7698 /* 30 - camels */

/* res spawns + distances adjusted, copy paste whole map */


/* Frozen Cove (4-25-20) by TheMadCADer */
/* Changes from 1.0: terrain changes for DE compatability, less zone avoidance on smaller maps */
/* Changes from 1.1: smaller base size in DE */
/* Changes from 1.2: Extra gold/stone doesn't spawn on high ground */
	
<PLAYER_SETUP>
grouped_by_team
	
<LAND_GENERATION>
base_terrain ICE
create_land
{
	terrain_type SNOW
	land_percent 100
	top_border 18
	bottom_border 18
	right_border 18
	left_border 18
}
create_player_lands
{
	terrain_type SNOW
	land_percent 15
	if 0_TEAM_GAME
		if 2_PLAYER_GAME
			top_border 12
			bottom_border 12
			right_border 12
			left_border 12
		else
			top_border 8
			bottom_border 8
			right_border 8
			left_border 8
		endif
	else
		top_border 12
		bottom_border 12
		right_border 12
		left_border 12
		if DE_AVAILABLE
			circle_radius 32 2
		endif
	endif
	if TINY_MAP
		base_elevation 6
	else
		base_elevation 7
	endif
	set_zone_by_team
	if TINY_MAP
		other_zone_avoidance_distance 12
	else
		other_zone_avoidance_distance 25
	endif
	clumping_factor 15
	base_size 14
}
create_land
{
	terrain_type ICE
	land_position 99 99
	land_percent 0
	base_size 1
	land_id 1
}
	
<ELEVATION_GENERATION>
if TINY_MAP
	create_elevation 4
else
	create_elevation 5
endif
{
	base_terrain ICE
	if DE_AVAILABLE
		number_of_tiles 100000
	else
		number_of_tiles 10000
	endif
	number_of_clumps 1000
	set_scale_by_groups
}
	
<TERRAIN_GENERATION>
create_terrain ICE
{
	base_terrain SNOW
	land_percent 100
	number_of_clumps 100
	set_scale_by_groups
	if TINY_MAP
		height_limits 0 5
	else
		height_limits 0 6
	endif
}
create_terrain MED_WATER
{
	base_terrain ICE
	land_percent 1
	number_of_clumps 4
	set_scale_by_groups
	height_limits 0 0
	set_flat_terrain_only
	spacing_to_other_terrain_types 1
}
create_terrain SNOW
{
	base_terrain ICE
	land_percent 100
	number_of_clumps 100
	set_scale_by_groups
	if TINY_MAP
		height_limits 6 6
	else
		height_limits 7 7
	endif
}
create_terrain SNOW_FOREST
{
	base_terrain SNOW
	land_percent 2
	number_of_clumps 8
	set_scale_by_groups
	set_avoid_player_start_areas
	spacing_to_other_terrain_types 1
}
create_terrain SNOW
{
	base_terrain ICE
	land_percent 100
	number_of_clumps 100
	set_scale_by_groups
	set_flat_terrain_only
	spacing_to_other_terrain_types 1
	height_limits 1 7
}
create_terrain SNOW_FOREST
{
	base_terrain SNOW
	if 0_TEAM_GAME
		land_percent 8
		number_of_clumps 20
	else
		land_percent 10
		number_of_clumps 16
	endif
	set_scale_by_groups
	set_avoid_player_start_areas
	spacing_to_other_terrain_types 3
	if TINY_MAP
		height_limits 4 4
	else
		height_limits 5 5
	endif
}
create_terrain GRASS_SNOW
{
	base_terrain SNOW
	land_percent 10
	number_of_clumps 24
	set_scale_by_groups
}
	
<OBJECTS_GENERATION>
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}
create_object VILLAGER
{
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 6
}
create_object SCOUT
{
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 9
}
/* Barracks - nah */
/*
create_object BARRACKS
{
set_place_for_every_player
min_distance_to_players 8
max_distance_to_players 14
min_distance_group_placement 5
}
*/
create_object GOLD /* primary */
{
	number_of_objects 7
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 12
	min_distance_group_placement 7
	max_distance_to_other_zones 7
	avoid_forest_zone 2
}
create_object STONE /* primary */
{
	number_of_objects 5
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 12
	max_distance_to_players 15
	min_distance_group_placement 7
	max_distance_to_other_zones 7
	avoid_forest_zone 2
}
create_object GOLD /* secondary */
{
	number_of_objects 5
	set_tight_grouping
	group_placement_radius 3
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 12
	max_distance_to_players 15
	min_distance_group_placement 7
	max_distance_to_other_zones 7
	avoid_forest_zone 1
}
create_object STONE /* secondary */
{
	number_of_objects 4
	group_placement_radius 2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 20
	max_distance_to_players 26
	min_distance_group_placement 7
	max_distance_to_other_zones 7
	avoid_forest_zone 1
}
create_object GOLD /* extra */
{
	number_of_objects 3
	number_of_groups 5
	set_tight_grouping
	group_placement_radius 3
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 20
	min_distance_to_players 30
	max_distance_to_other_zones 5
	place_on_specific_land_id 1
}
create_object STONE /* extra */
{
	number_of_objects 3
	number_of_groups 2
	set_tight_grouping
	group_placement_radius 3
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 20
	min_distance_to_players 30
	max_distance_to_other_zones 5
	place_on_specific_land_id 1
}
if DEATH_MATCH
else
create_object SHEEP /* primary */
{
	number_of_objects 4
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 6
	find_closest
	max_distance_to_other_zones 12
}
create_object SHEEP /* secondary */
{
	number_of_objects 2
	number_of_groups 2
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 16
	max_distance_to_players 16
	max_distance_to_other_zones 12
}
create_object FORAGE
{
	number_of_objects 6
	set_tight_grouping
	group_placement_radius 1
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 12
	max_distance_to_other_zones 7
	min_distance_group_placement 4
	avoid_forest_zone 3
}
create_object BOAR
{
	number_of_objects 2
	set_place_for_every_player
	set_gaia_object_only
	min_distance_to_players 12
	max_distance_to_players 14
	set_circular_placement
	max_distance_to_other_zones 3
	set_circular_placement
}
create_object DEER
{
	number_of_objects 4
	set_place_for_every_player
	set_gaia_object_only
	set_loose_grouping
	group_placement_radius 2
	min_distance_to_players 16
	max_distance_to_players 20
	max_distance_to_other_zones 2
	min_distance_group_placement 4
	avoid_forest_zone 2
	set_circular_placement
}
create_object SNOW_PINE_TREE /* straggler trees */
{
	number_of_objects 2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 4
	max_distance_to_players 5
	min_distance_group_placement 2
}
create_object SNOW_PINE_TREE
{
	number_of_objects 3
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 8
	min_distance_group_placement 2
}
endif
create_object RELIC
{
	number_of_objects 1
	min_distance_to_players 30
	min_distance_to_map_edge 3
	set_place_for_every_player
	find_closest
	set_gaia_object_only
	temp_min_distance_group_placement 25
}
create_object SHORE_FISH
{
	number_of_objects 1000
	set_scaling_to_map_size
	temp_min_distance_group_placement 5
}
if REGICIDE /* regicide objects */
create_object VILLAGER
{
number_of_objects 7
set_place_for_every_player
min_distance_to_players 6
max_distance_to_players 6
}
create_object KING
{
set_place_for_every_player
min_distance_to_players 6
max_distance_to_players 6
}
create_object CASTLE
{
set_place_for_every_player
min_distance_to_players 10
max_distance_to_players 10
}
endif
