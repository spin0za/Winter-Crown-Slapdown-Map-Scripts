/* AoE2 Underground - Winter Crown Slapdown */
/* MadCADer Expanse */

/* Updated by Hestia for consistent farm spawn

	 Sample seeds of unfair spawn:
	 1737659692 (Red touches map edge)
*/

<PLAYER_SETUP>

/* Sling nerfs */
#const MOD_RESOURCE 1
#const ATTR_SET_TIME -1
#const TRIBUTE_INEFFICIENCY 46
#const COINAGE 23
effect_percent MOD_RESOURCE TRIBUTE_INEFFICIENCY ATTR_SET 200
effect_amount MODIFY_TECH COINAGE ATTR_SET_TIME 180

/* Boar lame nerf, boar does a little over 1/3 scout hp on hit now */
/* Scouts expected to have <1/3 hp left after lame (hit twice) */
#const BOAR 48
#const ATTR_ATTACK 9
effect_amount SET_ATTRIBUTE BOAR ATTR_ATTACK 7434 /* 29 - eagles */
effect_amount SET_ATTRIBUTE BOAR ATTR_ATTACK 2058 /* 8 - cavalry */
effect_amount SET_ATTRIBUTE BOAR ATTR_ATTACK 7698 /* 30 - camels */

/* res spawns + distances adjusted, copy paste whole map */

/* Expanse (9-30-20) by TheMadCADer */
	
<PLAYER_SETUP>
random_placement
	
<LAND_GENERATION>
base_terrain DIRT3
create_player_lands
{
	terrain_type DIRT3
	land_percent 0
	base_size 2
	base_elevation 5
	circle_radius 38 2
	other_zone_avoidance_distance 20
}
create_land
{
	terrain_type GRASS3
	land_position 50 50
	other_zone_avoidance_distance 20
	land_id 1
}
	
<ELEVATION_GENERATION>
create_elevation 3
{
	base_terrain GRASS3
	number_of_tiles 1250
	number_of_clumps 24
	set_scale_by_size
	enable_balanced_elevation
}
	
<TERRAIN_GENERATION>
#const GRASS_DRY 100
create_terrain DIRT3
{
	base_terrain GRASS3
	land_percent 100
	number_of_clumps 1000
	set_scale_by_groups
}
create_terrain GRASS
{
	base_terrain DIRT3
	land_percent 100
	number_of_clumps 1000
	set_scale_by_groups
	set_avoid_player_start_areas
}
create_terrain DLC_BOGLAND
{
	base_terrain GRASS
	land_percent 100
	number_of_clumps 1000
	set_scale_by_groups
	spacing_to_other_terrain_types 6
}
create_terrain GRASS_DRY
{
	base_terrain DLC_BOGLAND
	land_percent 100
	number_of_clumps 1000
	set_scale_by_groups
	spacing_to_other_terrain_types 2
}
create_terrain DIRT3
{
	base_terrain GRASS
	land_percent 100
	number_of_clumps 1000
	set_scale_by_groups
	spacing_to_other_terrain_types 1
}
create_terrain PINE_FOREST
{
	base_terrain DIRT3
	land_percent 3
	number_of_clumps 8
	set_scale_by_groups
	set_avoid_player_start_areas
	spacing_to_other_terrain_types 1
}
create_terrain PINE_FOREST
{
	base_terrain GRASS_DRY
	land_percent 10
	number_of_clumps 16
	set_scale_by_groups
	spacing_to_other_terrain_types 3
}
create_terrain WATER
{
	base_terrain GRASS_DRY
	land_percent 2
	number_of_clumps 4
	set_scale_by_groups
	spacing_to_other_terrain_types 3
}
create_terrain GRASS_DRY
{
	base_terrain DIRT3
	land_percent 100
	number_of_clumps 1000
	set_scale_by_groups
}
create_terrain DIRT3
{
	base_terrain GRASS_DRY
	land_percent 15
	number_of_clumps 24
	set_scale_by_groups
}
#const GRAVEL 102
create_terrain GRAVEL
{
	base_terrain GRASS_DRY
	land_percent 10
	number_of_clumps 24
	set_scale_by_groups
	terrain_mask 1
}
	
<CONNECTION_GENERATION>
create_connect_to_nonplayer_land
{
	replace_terrain GRASS GRASS_DRY
	replace_terrain PINE_FOREST GRASS_DRY
	terrain_size GRASS 3 0
	terrain_size PINE_FOREST 1 0
	terrain_cost GRASS 3
	terrain_cost DIRT3 3
	terrain_cost PINE_FOREST 7
}
	
<OBJECTS_GENERATION>
#const TEMP 278
#const F_PALISADE 119
create_object TEMP
{
	number_of_objects 9999
	set_place_for_every_player
	terrain_to_place_on GRASS
	max_distance_to_players 22
	second_object F_PALISADE
	actor_area 1
	actor_area_radius 1
}
create_object TOWN_CENTER
{
	set_place_for_every_player
	min_distance_to_players 0
	max_distance_to_players 0
}
create_object VILLAGER
{
	set_place_for_every_player
	min_distance_to_players 6
	max_distance_to_players 6
}
create_object SCOUT
{
	set_place_for_every_player
	min_distance_to_players 7
	max_distance_to_players 9
}
if REGICIDE /* regicide objects */
	create_object VILLAGER
	{
		number_of_objects 7
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 6
	}
	create_object KING
	{
		set_place_for_every_player
		min_distance_to_players 6
		max_distance_to_players 6
	}
	create_object CASTLE
	{
		set_place_for_every_player
		min_distance_to_players 10
		max_distance_to_players 10
	}
endif
create_object FARM
{
	number_of_objects 8
	set_place_for_every_player
	max_distance_to_players 4
  	find_closest
}
create_object OUTPOST
{
	number_of_objects 9999
	set_place_for_every_player
	actor_area_to_place_in 1
	temp_min_distance_group_placement 10
}
create_object GOLD /* primary */
{
	number_of_objects 4
	number_of_groups 2
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 10
	max_distance_to_players 12
	min_distance_group_placement 7
	temp_min_distance_group_placement 15
	avoid_forest_zone 1
}
create_object STONE /* primary */
{
	number_of_objects 3
	number_of_groups 2
	group_placement_radius 3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players 11
	max_distance_to_players 12
	min_distance_group_placement 7
	temp_min_distance_group_placement 15
	avoid_forest_zone 1
}
create_object GOLD /* extra */
{
	number_of_objects 3
	number_of_groups 12
	set_tight_grouping
	group_placement_radius 3
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 20
	min_distance_to_players 30
	max_distance_to_other_zones 7
	avoid_forest_zone 1
}
create_object STONE /* extra */
{
	number_of_objects 3
	number_of_groups 8
	set_tight_grouping
	group_placement_radius 3
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_group_placement 3
	temp_min_distance_group_placement 20
	min_distance_to_players 30
	max_distance_to_other_zones 7
	avoid_forest_zone 1
}
if DEATH_MATCH
else
create_object FORAGE
{
	number_of_objects 6
	number_of_groups 100
	set_tight_grouping
	group_placement_radius 4
	set_gaia_object_only
	temp_min_distance_group_placement 22
	set_scaling_to_map_size
	min_distance_to_players 22
	avoid_actor_area 1
	min_distance_group_placement 6
	avoid_forest_zone 1
}
create_object DEER
{
	number_of_objects 3
	number_of_groups 100
	set_gaia_object_only
	set_loose_grouping
	temp_min_distance_group_placement 22
	set_scaling_to_map_size
	min_distance_to_players 22
	avoid_actor_area 1
}
endif
create_object RELIC
{
	number_of_objects 1
	min_distance_to_players 30
	min_distance_to_map_edge 3
	set_place_for_every_player
	find_closest
	set_gaia_object_only
	temp_min_distance_group_placement 25
}
create_object SHORE_FISH
{
	number_of_objects 100
	set_scaling_to_map_size
	temp_min_distance_group_placement 6
}
